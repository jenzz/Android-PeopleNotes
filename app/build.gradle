plugins {
    id 'com.android.application'
    id 'com.google.devtools.ksp' version '1.6.10-1.0.3'
    id 'com.squareup.sqldelight'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.jenzz.peoplenotes"
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            signingConfig debug.signingConfig
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    testOptions {
        unitTests {
            all {
                useJUnitPlatform()
            }
            includeAndroidResources = true
        }
    }

    sourceSets {
        String commonTestDir = 'src/commonTest/java'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }
}

kapt {
    correctErrorTypes true
}

kotlin {
    sourceSets {
        debug {
            kotlin.srcDir("build/generated/ksp/debug/kotlin")
        }
        release {
            kotlin.srcDir("build/generated/ksp/release/kotlin")
        }
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8.1'

    implementation 'androidx.activity:activity-compose:1.4.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-compose:2.4.1'
    implementation 'com.github.vestrel00:contacts-android:0.1.10'
    implementation 'com.google.android.material:material:1.5.0'

    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"

    implementation "com.squareup.sqldelight:android-driver:$sqldelight_version"
    implementation "com.squareup.sqldelight:coroutines-extensions-jvm:$sqldelight_version"

    implementation "com.google.dagger:hilt-android:$dagger_hilt_version"
    kapt "com.google.dagger:hilt-compiler:$dagger_hilt_version"

    implementation "io.github.raamcosta.compose-destinations:animations-core:$compose_destinations_version"
    ksp "io.github.raamcosta.compose-destinations:ksp:$compose_destinations_version"
}

dependencies {
    testImplementation 'app.cash.turbine:turbine:0.7.0'

    // JUnit 5
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"

    // JUnit 4
    testImplementation 'junit:junit:4.13.2'
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$junit_version"

    testImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    testImplementation "com.google.dagger:hilt-android-testing:$dagger_hilt_version"
    testImplementation 'com.google.truth:truth:1.1.3'
    testImplementation "com.squareup.sqldelight:sqlite-driver:$sqldelight_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_test_version"
    testImplementation 'org.robolectric:robolectric:4.7.3'

    kaptTest "com.google.dagger:hilt-compiler:$dagger_hilt_version"
}

dependencies {
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation "com.google.dagger:hilt-android-testing:$dagger_hilt_version"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_test_version"

    kaptAndroidTest "com.google.dagger:hilt-compiler:$dagger_hilt_version"
}
